<!DOCTYPE html>
<html>

<head>
	<title>WebGPU Sandbox</title>
</head>

<body>
	<main id="main"></main>

	<script id="shaders" type="text/wgsl">
		@group(0) @binding(0) var<uniform> ambient: vec3f;

		struct View {
			view: mat4x4f,
			proj: mat4x4f,
			viewProj: mat4x4f
		}
		@group(1) @binding(0) var<uniform> view: View;

		struct Camera {
			position: vec3f,
			direction: vec3f
		};
		@group(1) @binding(1) var<uniform> camera: Camera;

		struct Model {
			transform: mat4x4f,
			normal: mat3x3f
		}
		@group(2) @binding(0) var<uniform> model: Model;

		struct VertexOut {
			@builtin(position) fragment: vec4f,
			@location(0) position: vec4f,
			@location(1) normal: vec4f,
			@location(2) color: vec3f
		}

		@vertex
		fn vertexShader(
			@location(0) position: vec3f,
			@location(1) normal: vec3f,
			@location(2) color: vec3f
		) -> VertexOut {
			let modelPosition = vec4f(position, 1) * model.transform;
			return VertexOut(
				modelPosition * view.viewProj,
				modelPosition,
				vec4f(normal * model.normal, 1),
				color
			);
		}

		const lightPos = vec3f(-1, 1, 2);
		const lightCol = vec3f(0.9, 0.9, 0.8);
		// const lightAtten = vec3f(1, 0.22, 0.20);
		const lightAtten = vec3f(1, 0.14, 0.07);

		const modelDiffuse = 1;
		const modelSpecular = 1;
		const modelShininess = 1;

		@fragment
		fn fragmentShader(
			@builtin(position) fragment: vec4f,
			@location(0) position: vec4f,
			@location(1) normal: vec4f,
			@location(2) color: vec3f
		) -> @location(0) vec4f {
			let normalDir = normalize(normal.xyz);
			let lightVec = lightPos - position.xyz;
			let lightDir = normalize(lightVec);
			let viewDir = normalize(camera.position - position.xyz);
			let midDir = normalize(lightDir + viewDir);

			// Distance attenuation
			let distance = length(lightVec);
			let attenuation = 1.0 / (
				lightAtten.x +
				lightAtten.y * distance +
				lightAtten.z * (distance * distance)
			);

			// Diffuse - light/normal
			let diffuse = max(dot(lightDir, normalDir), 0.0);
			let diffuseCol = modelDiffuse * diffuse * lightCol;

			// Specular - Blinn
			let specular = pow(max(dot(normalDir, midDir), 0.0), modelShininess);
			let specularCol = modelSpecular * specular * lightCol;

			return vec4f((ambient + attenuation * (diffuseCol + specularCol)) * color, 1);
		}
	</script>

	<script src="dist/index.js"></script>

</body>

</html>